buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "com.wynprice.cursemaven" version "2.1.1"
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

repositories {
    maven {
        name = "Kotlin Early Access"
        url = "https://dl.bintray.com/kotlin/kotlin-eap"
    }
    maven {
        name = 'kotlinforforge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }
}

version = '1.0'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: "${mcp_mappings}"

    runs {
        client {
            workingDirectory project.file('run')
            properties 'mixin.env.disableRefMap': 'true'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                ArcanoxLib {
                    source project(':ArcanoxLib').sourceSets.main
                }
                TechMod {
                    source project(':TechMod').sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            properties 'mixin.env.disableRefMap': 'true'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            mods {
                ArcanoxLib {
                    source project(':ArcanoxLib').sourceSets.main
                }
                TechMod {
                    source project(':TechMod').sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            properties 'mixin.env.disableRefMap': 'true'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--all', '--output', file('src/generated/resources/'), '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                ArcanoxLib {
                    source project(':ArcanoxLib').sourceSets.main
                }
                TechMod {
                    source project(':TechMod').sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Forge itself
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Kotlin stuff
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.72"
    implementation 'thedarkcolour:kotlinforforge:1.6.2'

    // Runtime third-party mods
    runtimeOnly fg.deobf("curse.maven:mcjtylib:3097756")
    runtimeOnly fg.deobf("curse.maven:rftools-base:3087298")
    runtimeOnly fg.deobf("curse.maven:rftools-power:3087309")
    runtimeOnly fg.deobf("curse.maven:xnet:3096839")

    // Workspace mods
    compile project(':ArcanoxLib')
    compile project(':TechMod')
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
